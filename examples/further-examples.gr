-- This is a stripped down version of `further-examples.md.gr` without all the explanation.
-- But this is useful for experimenting with in `grepl`.

-- $ grepl
-- > :l examples/further-examples.gr

pushPair : forall {a : Type, b : Type, c : Nat} . (a × b) [c] -> a [c] × b [c]
pushPair [(x, y)] = ([x], [y])

pullPair : forall {a : Type, b : Type, k : Coeffect, c : k} . a [c] × b [c] -> (a × b) [c]
pullPair ([x], [y]) = [(x, y)]

import Vec

-- called `head` in the `Vec` library, but renamed here for example
safeHead : forall {a : Type, n : Nat} . (Vec (n+1) a) [0..1] -> a
safeHead [Cons x _] = x

-- Stack example (see StdLib/Stack.gr)

pop : forall {n : Nat, a : Type} . Vec (n+1) a -> (a, Vec n a)
pop = uncons

push : forall {n : Nat, a : Type} . a -> Vec n a -> Vec (n+1) a
push = Cons

peek : forall {n : Nat, a : Type} . (Vec (n+1) a) [1..2] -> (a, Vec (n+1) a)
peek [Cons x xs] = (x, Cons x xs)

peek' : forall {m : Ext Nat, a, n : Nat} . Vec (n+1) (a [m..m+1]) -> (a, Vec (n+1) (a [m..m]))
peek' (Cons [x] xs) = (x, Cons [x] xs)

-- Patient database example

data Patient = Patient (String [Public]) (String [Private])

sampleCities : forall {n : Nat, k : Nat} . N k -> (Vec (n+k) Patient) [0..1] -> Vec k (String [Public])
sampleCities Z [_] = Nil;
sampleCities (S n) [Cons (Patient [city] [name]) ps] = Cons [city] (sampleCities n [ps])

-- getCitiesBad : forall n. Vec n (Patient [0..1])  -> Vec n (String [Public])
-- getCitiesBad Nil = Nil;
-- getCitiesBad (Cons [Patient [city] [name]] ps) = Cons [name] (getCitiesBad ps)

-- Session types example

sendVec : forall {n : Nat, a} .
        (Chan (Send a End)) [n]
     -> Vec n a -> () <Session>
sendVec [c] Nil = pure ();
sendVec [c] (Cons x xs) =
  let c'  <- gsend c x;
      ()  <- gclose c'
  in sendVec [c] xs

recvVec : forall {n : Nat, a} .
  N n -> (Chan (Recv a End)) [n] -> (Vec n a) <Session>
recvVec Z [c] = pure Nil;
recvVec (S n) [c] =
  let (x, c') <- grecv c;
      ()      <- gclose c';
      xs      <- recvVec n [c]
  in pure (Cons x xs)

example : forall {n : Nat, a : Type} . N n -> Vec n a -> (Vec n a) <Session>
example n list = let c <- fork (\c -> sendVec c list) in recvVec n c
